;// set up hardware before starting the kernel
;// hw detection
#include <oric.h>
#include <system.h>

#include <ksym.h>

; we won't be changing disc drive after boot...
; so we copy simple setmemconf code to avoid useless tests in the kernel.
; we'll copy to the bootloader's copy and the kernel's copy of setmemconf_ovl/rom

#begindef MY_MEMCPY(to,from,sz)
		tya
		pha
		ldy #sz
	-	lda from,y
		sta to,y
		dey
		bne -
		pla
		tay
#enddef

#begindef MY_MEMCPYI(ito,from,sz)
		tya
		pha
		lda ito
		sta *+15
		lda ito+1
		sta *+10
		ldy #sz
	-	lda from,y
		sta $0,y
		dey
		bne -
		pla
		tay
#enddef

		;; disable all IRQs from VIA
		
		lda #$7f
		sta VIA1_IER
		
		;; detect model
		
		ldy #loric_atmos		; assume Atmos for now
		;; check for the "1" from "BASIC v1.1" of Atmos
		;lda #"1"
		;cmp $EDAD
		;; this check is used by Loriciel, might be more reliable (?)
		lda $fff9
		cmp #$01
		beq +
		ldy #loric_oric1
	+	
		jmp check_jasmin

smc_ptrr:
		.word lkf_oric_setmemconf_rom
smc_ptro:
		.word lkf_oric_setmemconf_ovl

		;; detect floppy hardware
		;; and enable overlay RAM
		
check_jasmin:
#ifdef SUPPORT_JASMIN
		;; detect Jasmin drive
		lda VIA1_ACR			; VIA1+11
		tax						; backup
		cmp JASMIN_ROMDIS		; 
		bne has_jasmin
		
		eor #%00000100			; change shift reg mode (unused)
		sta VIA1_ACR
		cmp JASMIN_ROMDIS		; 
		stx VIA1_ACR			; change back (flags unaffected)
		bne has_jasmin
		jmp check_microdisc
		
has_jasmin:
		MY_MEMCPY(oric_setmemconf_rom, rom_j, 9)
		MY_MEMCPY(oric_setmemconf_ovl, ovl_j, 9)
		
	;	MY_MEMCPY(lkf_oric_setmemconf_rom, rom_j, 9)
		MY_MEMCPYI(smc_ptrr, rom_j, 9)
		MY_MEMCPYI(smc_ptro, ovl_j, 9)
		tya
		ora #loric_jasmin
		tay
		jmp got_fdc				; who needs both ?
#endif
		
check_microdisc:
#ifdef SUPPORT_MICRODISC
		;; detect Microdrive/telestrat
		; probe MD
		ldx MICRODISC_IRQF
	-	lda VIA1_T1LO
		cmp VIA1_T1LO
		beq -
		cpx MICRODISC_IRQF
		bne check_telestrat
		
		MY_MEMCPY(oric_setmemconf_rom, rom_m, 9)
		MY_MEMCPY(oric_setmemconf_ovl, ovl_m, 9)
		MY_MEMCPYI(smc_ptrr , rom_m, 9)
		MY_MEMCPYI(smc_ptro , ovl_m, 9)
		tya
		ora #loric_mdisc
		tay
		
		jmp got_fdc

#endif
check_telestrat:
#ifdef SUPPORT_TELESTRAT
		;; check for 2nd VIA (stratos)
		lda VIA2_DDRA
		cmp #%00010111
		bne +
		cmp VIA1_DDRA
		beq +
		tya
		and #~loric_typemask
		ora #loric_stratos
		tay
		MY_MEMCPY(oric_setmemconf_rom, rom_t, 9)
		MY_MEMCPY(oric_setmemconf_ovl, ovl_t, 9)
		MY_MEMCPYI(smc_ptrr, rom_t, 9)
		MY_MEMCPYI(smc_ptro , ovl_t, 9)
		jmp got_fdc
	+	
#endif
		
		
		;; no floppy:
		;; display an error on status line, some fancy audio,
		;; wait some time and reboot.
no_fdc:
		lda #<_nodrivemsg
		;ldy #>_nodrivemsg ; delayed
		ldx #0
		
		; jump to ROM's STOUT
		; ORIC-1 vs Atmos: cf. Theoric nr 2 page 50
		cpy #loric_oric1
		bne no_fdc_atmos
		
		ldy #>_nodrivemsg
		jsr $f82f				; STOUT
		jsr $fab1				; EXPLD
		jmp +
no_fdc_atmos:
		ldy #>_nodrivemsg
		jsr $f865				; STOUD
		jsr $facb				; EXPLD
	+	
		; spin for a while...
		ldy #$ff
	-	dey
		bne -
		
		; RESET vector
		jmp ($fffc)

		;; we have a floppy, continue
got_fdc:
		; save model and hw flags
		sty lk_oric_arch
		
		;; set archtype (first solution on PAL/NTSC detection)

		ldx #larch_oric		; oric/ntsc
		stx lk_archtype
		
		; switch to overlay RAM
		jsr oric_setmemconf_ovl
		
		jmp resetdone


rom_j:	;9 bytes
		lda #%00000001			;
		sta JASMIN_OVERLAY		; enable overlay RAM
		sta JASMIN_ROMDIS		; disable BASIC ROM
		rts
ovl_j:	;9 bytes
		lda #%00000001			;
		sta JASMIN_OVERLAY		; enable overlay RAM
		sta JASMIN_ROMDIS		; disable BASIC ROM
		rts
rom_m:	;6 bytes
		; XXX: should have a ghost of the reg to avoid screwing up other bits...
		lda #%0000010			; enable EPROM and BASIC ROM
		sta MICRODISC_CONTROL
		
		rts
ovl_m:	;6 bytes
		;this can't work, reading doesn't give the same reg
		;lda MICRODISC_CONTROL
		;ora #%10000000			; disable EPROM
		;and #%11111101			; disable BASIC ROM
		lda #%10000000			; disable EPROM and BASIC ROM (also IRQs)
		sta MICRODISC_CONTROL
		rts
rom_t:	;9 bytes
		;; telestrat: select bank 7 (ROM)
		lda #%00000111
		ora VIA2_ORA
		sta VIA2_ORA
		rts
ovl_t:	;9 bytes
		;; telestrat: select bank 0 (RAM)
		lda #%11111000
		and VIA2_ORA
		sta VIA2_ORA
		rts


_nodrivemsg:
		.text "Jasmin or Microdisc required!"
		.byte $00
		
resetdone:
