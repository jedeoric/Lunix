;// set up hardware before starting the kernel
;// hw detection
#include <oric.h>
#include <system.h>

		;; disable all IRQs from VIA
		
		lda #$7f
		sta VIA1_IER
		
		;; detect model
		
		ldy #loric_atmos		; assume Atmos for now
		;; check for the "1" from "BASIC v1.1" of Atmos
		;lda #"1"
		;cmp $EDAD
		;; this check is used by Loriciel, might be more reliable (?)
		lda $fff9
		cmp #$01
		beq +
		ldy #loric_oric1
	+	
#ifdef SUPPORT_TELESTRAT
		;; check for 2nd VIA (stratos)
		lda VIA2_DDRA
		cmp #%00010111
		bne +
		cmp VIA1_DDRA
		beq +
		ldy #loric_stratos
		;; telestrat: select bank 0 (RAM)
		lda #%11111000
		and VIA2_ORA
		sta VIA2_ORA
	+	
#endif
		
		;; detect floppy hardware
		;; and enable overlay RAM
		
check_jasmin:
#ifdef SUPPORT_JASMIN
		;; detect Jasmin drive
		lda VIA1_ACR			; VIA1+11
		tax						; backup
		cmp JASMIN_ROMDIS		; 
		bne has_jasmin
		
		eor #%00000100			; change shift reg mode (unused)
		sta VIA1_ACR
		cmp JASMIN_ROMDIS		; 
		stx VIA1_ACR			; change back (flags unaffected)
		bne has_jasmin
		jmp check_microdisc
		
has_jasmin:
		lda #%00000001			;
		sta JASMIN_OVERLAY		; enable overlay RAM
		sta JASMIN_ROMDIS		; disable BASIC ROM

		tya
		ora #loric_jasmin
		tay
		jmp got_fdc				; who needs both ?
#endif
		
check_microdisc:
#ifdef SUPPORT_MICRODISC
		;; detect Microdrive/telestrat
		; probe MD
		ldx MICRODISC_IRQF
	-	lda VIA1_T1LO
		cmp VIA1_T1LO
		beq -
		cpx MICRODISC_IRQF
		bne no_fdc
		
		;this can't work, reading doesn't give the same reg
		;lda MICRODISC_CONTROL
		;ora #%10000000			; disable EPROM
		;and #%11111101			; disable BASIC ROM
		
		lda #%10000000			; disable EPROM and BASIC ROM (also IRQs)
		sta MICRODISC_CONTROL
		
		tya
		ora #loric_mdisc
		tay
		
		jmp got_fdc
#endif
		
		;; no floppy:
		;; display an error on status line, some fancy audio,
		;; wait some time and reboot.
no_fdc:
		lda #<_nodrivemsg
		;ldy #>_nodrivemsg ; delayed
		ldx #0
		
		; jump to ROM's STOUT
		; ORIC-1 vs Atmos: cf. Theoric nr 2 page 50
		cpy #loric_oric1
		bne no_fdc_atmos
		
		ldy #>_nodrivemsg
		jsr $f82f				; STOUT
		jsr $fab1				; EXPLD
		jmp +
no_fdc_atmos:
		ldy #>_nodrivemsg
		jsr $f865				; STOUD
		jsr $facb				; EXPLD
	+	
		; spin for a while...
		ldy #$ff
	-	dey
		bne -
		
		; RESET vector
		jmp ($fffc)

		;; we have a floppy, continue
got_fdc:
		; save model and hw flags
		sty lk_oric_arch
		
		;; set archtype (first solution on PAL/NTSC detection)

		ldx #larch_oric		; oric/ntsc
		stx lk_archtype
		jmp resetdone

_nodrivemsg:
		.text "Jasmin or Microdisc required!"
		.byte $00
		
resetdone:
